; operator precedence
; a[...]
; - (minus)
; /
; *
; + - (subtract)
; == != < > <= >=
; !
; &
; ^
; |

<expression>        ::= <xor_expression> "|" <expression>      | <xor_expression>
<xor_expression>    ::= <and_expression> "^" <xor_expression>  | <and_expression>
<and_expression>    ::= <not_expression> "&" <and_expression>  | <not_expression>

<not_expression>    ::= "!" <equal_expression> | <equal_expression>

<equal_expression>  ::= <plus_expression> "==" <equal_expression> | <plus_expression> "!=" <equal_expression> |
                        <plus_expression> ">"  <equal_expression> | <plus_expression> "<"  <equal_expression> | 
                        <plus_expression> "<=" <equal_expression> | <plus_expression> "<=" <equal_expression> | <plus_expression>

<plus_expression>   ::= <times_expression> "+" <plus_expression>  | <times_expression> "-" <plus_expression> | <times_expression>

<times_expression>  ::= <divide_expression> "*" <times_expression>  | <divide_expression>
<divide_expression> ::= <minus_expression>  "/" <divide_expression> | <minus_expression>

<minus_expression>  ::= "-" <function_call> | <function_call>
<function_call>     ::= <name> "[" <expression> ("," <expression>)* "]" | <term>

<term>   ::= "(" <expression> ")" | <name> | <number>
<number> ::= <digit>*
<name>   ::= <letter> (<letter> | <digit>)*

<letter> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" |
             "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
<digit>  ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
